plugins {
    id 'groovy'
    id 'codenarc'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

group = 'org.merscwog'
version = '1.0'

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }
}

String groovyVersion = '4.0.2'
String codeNarcGroovyVersion = '3.0.9'

dependencies {
    constraints {
        implementation "org.apache.groovy:groovy:${groovyVersion}"
    }

    modules {
        module('org.codehaus.groovy:groovy') {
            replacedBy('org.apache.groovy:groovy', 'new org since groovy 4.x')
        }
    }

    implementation 'org.apache.groovy:groovy'
}

testing {
    suites {
        test {
            useSpock('2.1-M2-groovy-3.0')
        }
    }
}

// Spock hack to make it able to run against Groovy 4
// Force @CompileStatic default and possibly other defaults by DefaultCompileAST
// Set Java 8 target version to make it easier to use existing Java installations on destination computers
tasks.withType(GroovyCompile).configureEach { GroovyCompile groovyCompile ->
    groovyCompile.options.forkOptions.jvmArgs << '-Dspock.iKnowWhatImDoing.disableGroovyVersionCheck=true'
    if (! groovyCompile.name.contains('Test')) {
        groovyCompile.targetCompatibility = '1.8'
        groovyOptions.configurationScript = rootProject.file('buildSrc/src/main/groovy/DefaultCompileAST.groovy')
    }
}

// Spock hack needs to be replicated at run phase sometimes
tasks.withType(Test).configureEach { Test test ->
    test.systemProperties('spock.iKnowWhatImDoing.disableGroovyVersionCheck': true)
}

// CodeNarc 3.0.0 as expected works with groovy 3.x, but still fails on groovy 4
configurations.codenarc {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.codehaus.groovy') {
            //details.useTarget("org.apache.groovy:${details.requested.name}:${groovyVersion}")
            details.useVersion(codeNarcGroovyVersion)
        }
    }
}

codenarc {
    config = rootProject.resources.text.fromFile('buildSrc/src/main/groovy/CodeNarcRules.groovy')
    toolVersion = '3.0.0'
}

tasks.withType(CodeNarc) { CodeNarc codeNarc ->
    SourceSetContainer sourceSetContainer = codeNarc.project.extensions.getByType(JavaPluginExtension).sourceSets
    SourceSet main = sourceSetContainer.getByName(SourceSet.MAIN_SOURCE_SET_NAME)
    SourceSet test = sourceSetContainer.getByName(SourceSet.TEST_SOURCE_SET_NAME)

    FileCollection totalCompilationClasspath = main.compileClasspath + main.output
    if (codeNarc.name.contains('Test')) {
        totalCompilationClasspath = test.compileClasspath + test.output
    }

    codeNarc.compilationClasspath = totalCompilationClasspath
}
